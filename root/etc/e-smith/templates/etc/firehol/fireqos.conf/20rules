{
  use esmith::NetworksDB;
  use esmith::ConfigDB;
  use NethServer::Firewall;
 
  my $ndb = esmith::NetworksDB->open_ro() || die("Can't open networks db");
  my $tdb = esmith::ConfigDB->open_ro('tc') || die("Can't open networks TC db");
  my $fw = new NethServer::Firewall();

  my @providers = $fw->getProviders();
  my @classes = $tdb->get_all_by_prop('type' => 'class');

  sub getProviderName
  {
      my $needle = shift;
      foreach my $p (@providers) {
          if ($p->{'interface'} eq $needle) {
              return $p->{'name'};
          }
      }
      return "red-$needle";
  }

  sub outRate
  {
      my $direction = shift;
      my $min = shift;
      my $max = shift;
      my $unit = shift;
      my $out = '';

      if ($min || $max) {
          $out .= "$direction";
          if ($min > 0) {
              $out .= " commit $min$unit";
          }
          if ($max > 0) {
              $out .= " ceil $max$unit";
          }
      }

      return $out;
  }

  foreach my $red ($ndb->red()) {
      my $interface = $red->key;
      my $in = $red->prop('FwInBandwidth') || next;
      my $out = $red->prop('FwOutBandwidth') || next;
      my $linklayer = $red->prop('TCLinklayer') || 'ethernet';

      $OUT .= "\n\n";

      $OUT .= "interface $interface ".getProviderName($interface)." bidirectional input rate ${in}kbit output rate ${out}kbit balanced $linklayer\n";

      foreach my $class (@classes) {
          my $name = $class->key;
          my $mark = $class->prop('Mark') || next;
          my $min_out = $class->prop('MinOutputRate') || 0;
          my $max_out = $class->prop('MaxOutputRate') || 0;
          my $min_in = $class->prop('MinInputRate') || 0;
          my $max_in = $class->prop('MaxInputRate') || 0;
          my $unit = $class->prop('Unit') || '%';
          my $bindto = $class->prop('BindTo') || '';
          if ($bindto ne '') {
              my %binds = map { $_ => 1 } split(",",$bindto);
              next if (!exists($binds{$interface}));
          }

          # skip empty classes
          next if (!$min_out && !$max_out && !$min_in && !$max_in);

          $OUT .= "\n";
          $OUT .= "\tclass $name ".outRate('input',$min_in,$max_in,$unit)." ".outRate('output',$min_out,$max_out,$unit)."\n";
          $OUT .= "\t\tmatch connmark 0x$mark\n";
      }

  }
}
